; ~*~ Lasciate ogne speranza, voi ch'intrate. ~*~

; This file contains the ABNF rules for the IRC grammer as described in RFC 2812.
; See: https://datatracker.ietf.org/doc/html/rfc2812
; See: https://en.wikipedia.org/wiki/Augmented_Backus-Naur_form

; ~*~ The following rules have been modified from their originals. ~*~

; Use 30 characters instead of the 9 in the specification.
; See: https://datatracker.ietf.org/doc/html/rfc2812#section-1.2.1
nickname   =  ( letter / special ) *29( letter / digit / special / "-" )

; Pushing trailing space down into optional segments, possibly a limitation of the parser?
message    =  [ ":" prefix ] command [ params ] crlf
prefix     =  (servername SPACE) / ( nickname [ [ "!" user ] "@" host ] SPACE )

; ~*~ Copyright notice included with RFC 2812 ~*~

; Copyright (C) The Internet Society (2000).  All Rights Reserved.
;
; This document and translations of it may be copied and furnished to
; others, and derivative works that comment on or otherwise explain it
; or assist in its implementation may be prepared, copied, published
; and distributed, in whole or in part, without restriction of any
; kind, provided that the above copyright notice and this paragraph are
; included on all such copies and derivative works.  However, this
; document itself may not be modified in any way, such as by removing
; the copyright notice or references to the Internet Society or other
; Internet organizations, except as needed for the purpose of
; developing Internet standards in which case the procedures for
; copyrights defined in the Internet Standards process must be
; followed, or as required to translate it into languages other than
; English.
;
; The limited permissions granted above are perpetual and will not be
; revoked by the Internet Society or its successors or assigns.
;
; This document and the information contained herein is provided on an
; "AS IS" basis and THE INTERNET SOCIETY AND THE INTERNET ENGINEERING
; TASK FORCE DISCLAIMS ALL WARRANTIES, EXPRESS OR IMPLIED, INCLUDING
; BUT NOT LIMITED TO ANY WARRANTY THAT THE USE OF THE INFORMATION
; HEREIN WILL NOT INFRINGE ANY RIGHTS OR ANY IMPLIED WARRANTIES OF
; MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.

; ~*~ The following modifications have been made to the RFC2812 content below. ~*~
; - Relevant published errata changes have been applied.
; - The =/ rule adding to params was missing a leading 'params' definition.
; - The changed rules above have been commented out to avoid redefinition.

; ~*~ Below is the reproduction of RFC2812 Section 2.3.1 ~*~

; 2.3.1 Message format in Augmented BNF
;
; The protocol messages must be extracted from the contiguous stream of
; octets.  The current solution is to designate two characters, CR and
; LF, as message separators.  Empty messages are silently ignored,
; which permits use of the sequence CR-LF between messages without
; extra problems.
;
; The extracted message is parsed into the components <prefix>,
; <command> and list of parameters (<params>).
;
;  The Augmented BNF representation for this is:

; message    =  [ ":" prefix SPACE ] command [ params ] crlf
; prefix     =  servername / ( nickname [ [ "!" user ] "@" host ] )
command    =  1*letter / 3digit
params     =  *14( SPACE middle ) [ SPACE ":" trailing ]
params     =/ 14( SPACE middle ) [ SPACE [ ":" ] trailing ]

nospcrlfcl =  %x01-09 / %x0B-0C / %x0E-1F / %x21-39 / %x3B-FF
                ; any octet except NUL, CR, LF, " " and ":"
middle     =  nospcrlfcl *( ":" / nospcrlfcl )
trailing   =  *( ":" / " " / nospcrlfcl )

SPACE      =  %x20        ; space character
crlf       =  %x0D %x0A   ; "carriage return" "linefeed"

; NOTES:
;    1) After extracting the parameter list, all parameters are equal
;       whether matched by <middle> or <trailing>. <trailing> is just a
;       syntactic trick to allow SPACE within the parameter.
;
;    2) The NUL (%x00) character is not special in message framing, and
;       basically could end up inside a parameter, but it would cause
;       extra complexities in normal C string handling. Therefore, NUL
;       is not allowed within messages.
;
; Most protocol messages specify additional semantics and syntax for
; the extracted parameter strings dictated by their position in the
; list.  For example, many server commands will assume that the first
; parameter after the command is the list of targets, which can be
; described with:

target     =  nickname / servername
msgtarget  =  msgto *( "," msgto )
msgto      =  channel / ( user [ "%" host ] "@" servername )
msgto      =/ ( user "%" host ) / targetmask
msgto      =/ nickname / ( nickname "!" user "@" host )
channel    =  ( "#" / "+" / ( "!" channelid ) / "&" ) chanstring
              [ ":" chanstring ]
servername =  hostname
host       =  hostname / hostaddr
hostname   =  shortname *( "." shortname )
shortname  =  ( letter / digit ) *( letter / digit / "-" )
              *( letter / digit )
                ; as specified in RFC 1123 [HNAME]
hostaddr   =  ip4addr / ip6addr
ip4addr    =  1*3digit "." 1*3digit "." 1*3digit "." 1*3digit
ip6addr    =  1*hexdigit 7( ":" 1*hexdigit )
ip6addr    =/ "0:0:0:0:0:" ( "0" / "FFFF" ) ":" ip4addr
; nickname   =  ( letter / special ) *8( letter / digit / special / "-" )
targetmask =  ( "$" / "#" ) mask
                ; see details on allowed masks in section 3.3.1
chanstring = *49(%x01-06 / %x08-09 / %x0B-0C / %x0E-1F / %x21-2B /
           %x2D-39 / %x3B-FF)
                ; any octet except NUL, BELL, CR, LF, " ", "," and ":"
channelid  = 5( %x41-5A / digit )   ; 5( A-Z / 0-9 )

; Other parameter syntaxes are:

user       =  1*( %x01-09 / %x0B-0C / %x0E-1F / %x21-3F / %x41-FF )
                ; any octet except NUL, CR, LF, " " and "@"
key        =  1*23( %x01-05 / %x07-08 / %x0C / %x0E-1F / %x21-7F )
                ; any 7-bit US_ASCII character,
                ; except NUL, CR, LF, ACK, h/v TABs, and " "
letter     =  %x41-5A / %x61-7A       ; A-Z / a-z
digit      =  %x30-39                 ; 0-9
hexdigit   =  digit / "A" / "B" / "C" / "D" / "E" / "F"
special    =  %x5B-60 / %x7B-7D
                 ; "[", "]", "\", "`", "_", "^", "{", "|", "}"

; NOTES:
;     1) The <hostaddr> syntax is given here for the sole purpose of
;        indicating the format to follow for IP addresses.  This
;        reflects the fact that the only available implementations of
;        this protocol uses TCP/IP as underlying network protocol but is
;        not meant to prevent other protocols to be used.
;
;     2) <hostname> has a maximum length of 63 characters.  This is a
;        limitation of the protocol as internet hostnames (in
;        particular) can be longer.  Such restriction is necessary
;        because IRC messages are limited to 512 characters in length.
;        Clients connecting from a host which name is longer than 63
;        characters are registered using the host (numeric) address
;        instead of the host name.
;
;     3) Some parameters used in the following sections of this
;        documents are not defined here as there is nothing specific
;        about them besides the name that is used for convenience.
;        These parameters follow the general syntax defined for
;        <params>.
